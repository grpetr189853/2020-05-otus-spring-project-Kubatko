plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'application'
    id 'org.springframework.boot' version '2.3.3.RELEASE'
}

apply plugin: 'io.spring.dependency-management'

group = 'ru.skubatko.dev.ess'
description = 'ui'
version = '0.0.1'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

mainClassName = 'ru.skubatko.dev.ees.ui.EesMainApp'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    jcenter()
}

ext {
    set('springCloudVersion', "Hoxton.SR8")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    implementation 'org.springframework.cloud:spring-cloud-starter'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-zuul'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-hystrix'
    implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'
    implementation 'org.springframework.cloud:spring-cloud-starter-zipkin'
    implementation("org.springframework.cloud:spring-cloud-config-client")

    implementation 'org.webjars:bootstrap:3.3.7'
    implementation 'org.webjars:jquery:2.2.4'
    implementation 'org.webjars:webjars-locator:0.30'
    implementation 'org.webjars:font-awesome:5.4.1'

    implementation 'org.postgresql:postgresql'
    implementation 'org.liquibase:liquibase-core'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'com.h2database:h2'
}

idea {
    project {
        languageLevel = 11
    }
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs = ['-Xlint:deprecation']
}

bootJar {
    archiveVersion.set("")
    layered()
}

bootBuildImage {
    imageName = "ees/ui"
}

test {
    useJUnitPlatform()
    filter { excludeTestsMatching '*ITCase*' }
}

task integrationTest(type: Test) {
    group 'verification'
    description 'Integration tests'
    filter { includeTestsMatching '*ITCase*' }
    useJUnitPlatform()
}
